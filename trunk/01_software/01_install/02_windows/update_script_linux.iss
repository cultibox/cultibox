; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Cultibox"
#define MyOldAppVersion "1.0.264"
#define MyAppVersion "1.0.308"
#define MyAppPublisher "Green Box SAS"
#define MyAppURL "http://www.cultibox.fr/"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{E8DC2CCC-6FD8-4FA2-B11A-4CF206BA4C60}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={sd}\{#MyAppName}
DefaultGroupName={#MyAppName}
AllowNoIcons=yes
OutputBaseFilename=update_CultiBox_from_{#MyOldAppVersion}_to_{#MyAppVersion}
VersionInfoVersion={#MyAppVersion}
Compression=lzma
SolidCompression=yes
 ; Pas de warning si le dossier existe déjà
DirExistsWarning=no
; Interdiction de changer le path
DisableDirPage=yes
CreateUninstallRegKey=no
UpdateUninstallLogAppName=no

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"
Name: "french"; MessagesFile: "compiler:Languages\French.isl"


[CustomMessages]
french.ContinueUpdate=Vous êtes sur le point d'installer une mise à jour pour le logiciel CultiBox, voulez-vous continuer?
english.ContinueUpdate=You are going to install an update for the cultibox software, do you ant to continue?

french.WrongVersion=La mise à jour que vous tentez d'installer ne correspond pas à la version actuellement installée sur votre ordinateur
english.WrongVersion=The update you are trying to install doesn't match the version currently installed in your computer

french.SaveDatas=Sauvegarde de vos logs et programme puis installation de la mise à jour
english.SaveDatas=Save of your logs and programs then install the update

[code]
var 
  update: boolean;

function InitializeSetup():boolean;
var
  ResultCode: integer;

begin 
  if FileExists(ExpandConstant('{sd}\{#MyAppName}\xampp\VERSION_{#MyOldAppVersion}.txt')) then
  begin
     update := true;
  end else begin
     update := false;
  end;

  if FileExists(ExpandConstant('{sd}\{#MyAppName}\unins000.exe')) and (update) then
  begin
     if MsgBox(ExpandConstant('{cm:ContinueUpdate}'), mbConfirmation, MB_YESNO or MB_DEFBUTTON2) = IDYES then                                                                                                                                 
       begin
          MsgBox(ExpandConstant('{cm:SaveDatas}'), mbInformation, MB_OK);

          ExtractTemporaryFile ('backup.bat');

          Exec (ExpandConstant ('{cmd}'), ExpandConstant ('/C copy backup.bat {sd}\{#MyAppName}\backup.bat'), ExpandConstant ('{tmp}'), SW_SHOW, ewWaitUntilTerminated, ResultCode);

          Exec (ExpandConstant ('{cmd}'), '/C backup.bat', ExpandConstant ('{sd}\{#MyAppName}'), SW_SHOW, ewWaitUntilTerminated, ResultCode);

          Exec (ExpandConstant ('{cmd}'), ExpandConstant ('/C del {sd}\{#MyAppName}\xampp\VERSION_*'), ExpandConstant ('{tmp}'), SW_SHOW, ewWaitUntilTerminated, ResultCode);
          Exec (ExpandConstant ('{cmd}'), ExpandConstant ('/C del {sd}\{#MyAppName}\xampp\htdocs\cultibox\main\templates_c\l10n*'), ExpandConstant ('{tmp}'), SW_SHOW, ewWaitUntilTerminated, ResultCode);
          Result := True;    
       end else 
       begin
           Result := False;
       end;
  end else 
  begin
    MsgBox(ExpandConstant('{cm:WrongVersion}'), mbInformation, MB_OK);
    Result := False;
  end;
end;

procedure CurUninstallStepChanged(CurUninstallStep: TUninstallStep);
var
  ResultCode: Integer;
begin
   
   if CurUninstallStep = usUninstall then
   begin
      Exec (ExpandConstant ('{cmd}'), '/C net stop cultibox_apache', ExpandConstant ('{tmp}'), SW_SHOW, ewWaitUntilTerminated, ResultCode);
      Exec (ExpandConstant ('{cmd}'), '/C net stop cultibox_mysql', ExpandConstant ('{tmp}'), SW_SHOW, ewWaitUntilTerminated, ResultCode);
      Exec (ExpandConstant ('{cmd}'), '/C apache_uninstallservice.bat', ExpandConstant ('{sd}\{#MyAppName}\xampp\apache'), SW_SHOW, ewWaitUntilTerminated, ResultCode);
      Exec (ExpandConstant ('{cmd}'), '/C mysql_uninstallservice.bat', ExpandConstant ('{sd}\{#MyAppName}\xampp\mysql'), SW_SHOW, ewWaitUntilTerminated, ResultCode);
   end;
end;


[Files]
Source: "C:\users\yann\Desktop\Project\cultibox\01_software\01_install\01_src\03_sd\firm.hex"; DestDir: "{app}\xampp\htdocs\cultibox\tmp"; Flags: ignoreversion
Source: "C:\users\yann\Desktop\Project\cultibox\01_software\01_install\01_src\01_xampp\update_sql_windows_from_{#MyOldAppVersion}_to_{#MyAppVersion}.sql"; DestDir: "{app}\xampp\sql_install\"; Flags: ignoreversion
; Backup file. Used in pre install
Source: "C:\users\yann\Desktop\Project\cultibox\01_software\01_install\02_windows\conf-script\backup.bat"; DestDir: "{app}\run"; DestName: "backup.bat"; Flags: ignoreversion recursesubdirs createallsubdirs
; load file. Used in post install
Source: "C:\users\yann\Desktop\Project\cultibox\01_software\01_install\02_windows\conf-script\load.bat"; DestDir: "{app}\run"; Flags: ignoreversion recursesubdirs createallsubdirs
; NOTE: Don't use "Flags: ignoreversion" on any shared system files


[Run]
Filename: "{app}\xampp\mysql\bin\mysql.exe"; \
  Parameters: " -u root -h localhost --port=3891 -pcultibox cultibox -e ""source xampp\sql_install\update_sql_windows_from_{#MyOldAppVersion}_to_{#MyAppVersion}.sql"""; \
  WorkingDir: "{app}"; \
  Description: "Update DataBase";